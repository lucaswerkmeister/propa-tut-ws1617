\documentclass{beamer}
\usepackage{tut}

\def\tuttitle{List-Comprehensions}
\date{2016-11-07/08}

\begin{document}
\normalsize
\normalem

\begin{frame}[plain]
  \titlepage
\end{frame}

\begin{frame}[fragile]
  \frametitle{Aufgabe 1: Bindung und Gültigkeitsbereiche}
  \emph{Bindung} ist der Vorgang, einen \emph{Bezeichner} (\emph{Identifier}, Name) an eine Bedeutung zu binden.
  In \lstinline{f x = 1} wird \lstinline{x} an die Bedeutung „erster Parameter von \lstinline{f}“ gebunden.
  \lstinline{x} ist hier eine Bindungsstelle und \lstinline{f} eine Definitionsstelle.
\end{frame}

\begin{frame}[fragile]
  \frametitle{Aufgabe 1: Bindung und Gültigkeitsbereiche}
  \begin{lstlisting}
    f y = \z -> x + 7 * z - y
    x = 1
    g x = x + (let y = x * 2; x = 5 * 5
               in (let x = f x 2 in x + y))
    h = let z = 2 in g x + (\z -> -z) z where z = 3
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{add}}
  \begin{lstlisting}
    type Polynom = [Double]
  \end{lstlisting}
  Polynom $1 + 2 x + 3 x^2$ wird als Liste \lstinline{[1,2,3]} dargestellt.
  Definiere Polynomaddition \lstinline{add :: Polynom -> Polynom -> Polynom}.
  \pause
  \begin{lstlisting}
    add a [] = a
    add [] b = b
    add (a:as) (b:bs) = a + b : add as bs
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{\texttt{add}}
  Lösung mit Listenkombinatoren:
  \begin{lstlisting}
    add = zipWith (+)
  \end{lstlisting}
  \pause
  Leider \textbf{inkorrekt!} Funktioniert nicht für unterschiedlich lange Listen:
  \begin{lstlisting}
    add [1,2] [1] = [2]
    -- sollte aber sein:
                    [2,2]
  \end{lstlisting}
  denn \lstinline{zipWith} schneidet am Ende der kürzeren Liste ab,
  wir müssen aber den Rest der längeren Liste anhängen (gewissermaßen noch \lstinline{[0,0..]} addieren).
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{eval}}
  Auswertung des Polynoms an Stelle $x$ durch Horner-Schema:
  \[\texttt{eval [1,2,3] x }= 1 + x\cdot(2 + x\cdot(3 + x\cdot0))\]
  Welche Fold-Version bietet sich hier an?
  \pause
  \begin{lstlisting}
    eval p x = foldr (\a acc -> a + x * acc) 0 p
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{deriv}}
  Ableitung des Polynoms. Welcher Listenkombinator bietet sich an?
  \pause
  \begin{lstlisting}
    deriv [] = []
    deriv (_:p) = zipWith (*) p [1..]
  \end{lstlisting}
  Oder:
  \begin{lstlisting}
    deriv p = drop 1 $ zipWith (*) [0..] p
  \end{lstlisting}
  Oder:
  \begin{lstlisting}
    deriv = drop 1 . zipWith (*) [0..]
  \end{lstlisting}
  (Verwende \lstinline{drop 1}, weil \lstinline{tail []} einen Fehler ergibt, \lstinline{drop 1 []} hingegen nicht.)
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{atLeastElements}}
  \begin{lstlisting}
    atLeastElements :: [Int] -> Int -> Bool
  \end{lstlisting}
  Gibt zurück, ob mindestens $n$ Listenelemente $\geq n$ sind.
  \pause
  \begin{lstlisting}
    atLeastElements l n = length (filter (>=n) l) >= n
  \end{lstlisting}
  Oder (Beispiellösung):
  \begin{lstlisting}
    atLeastElements l n = length [m | m <- list, m >= n] >= n
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{List Comprehensions}
  Schreibweise zur Konstruktion von Listen.
  Eine List Comprehension hat zwei Teile:
  links einen Ausdruck,
  und rechts eine Reihe von Generatoren, Guards, und \texttt{let}s.
  \begin{lstlisting}
    [a + b | a<-[1..], b<-[1..a], -- Generatoren
             odd a, odd b,        -- Guards
             let p=a*b,           -- let
             p<1000]              -- Guard
  \end{lstlisting}
  Ergibt eine Liste mit den Ergebnissen des Ausdrucks für jede Kombination der Variablen aus den Generatoren, für die alle Guards erfüllt sind.
  Dabei werden die hinteren Generatoren zuerst durchlaufen:
  \begin{lstlisting}
    [(a,b) | a<-[1,2], b<-[1,2]] = [(1,1),(1,2),(2,1),(2,2)]
  \end{lstlisting}
  Wenn also der zweite Generator unendlich ist, wird das zweite Element des ersten Generators nie erreicht.
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{hIndexCorrect}}
  \begin{lstlisting}
    hIndexCorrect :: ([Int] -> Int) -> [Int] -> Bool
  \end{lstlisting}
  Prüft, ob die gegebene Funktion für die gegebene Liste tatsächlich die größte Zahl $n \leq $\lstinline{(length list)} zurückgibt,
  so das mindestens $n$ Listenelemente $\geq n$ sind.
  Verwende \lstinline{atLeastElements}.
  \pause
  \begin{lstlisting}
    hIndexCorrect hIndex l = atLeastElements l n
      && not (atLeastElements l (n+1))
      where n = hIndex l
  \end{lstlisting}
  Oder:
  \begin{lstlisting}
    hIndexCorrect hIndex l =
      all (atLeastElements l) [0..n] &&
      all (not . atLeastElements l) [n+1 .. length l]
      where n = hIndex l
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{hIndex}}
  \begin{lstlisting}
    hIndex :: [Int] -> Int
  \end{lstlisting}
  Berechnet größte Zahl $n$, so dass mindestens $n$ Listenelemente $\geq n$ sind.
  Vorgehen: sortiere Liste absteigend und zähle von vorne durch, bis das $i$. Element (ab 0 gezählt) $\leq i$ ist.
  Verwende \lstinline{sort} und \lstinline{reverse} aus \lstinline{Data.List}.
  \pause
  \begin{lstlisting}
    hIndex l = helper (reverse (sort l)) 0
      where helper (e:ls) i
              | e <= i    = i
              | otherwise = helper ls (i+1)
            helper [] i = i
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{hIndex}}
  Alternative Version:
  \begin{lstlisting}
    hIndex l =
      length
      (takeWhile (\(i, n) -> n > i)
      (zip [0..]
      (reverse
      (sort l))))
  \end{lstlisting}
  \pause
  Die meisten Klammern kann man mit \lstinline{$}-Schreibweise loswerden:
  \begin{lstlisting}
    hIndex l =
      length
      $ takeWhile (\(i, n) -> n > i)
      $ zip [0..]
      $ reverse
      $ sort l
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{hIndex}}
  \begin{lstlisting}
    hIndex l =
      length
      $ takeWhile (\(i, n) -> n > i)
      $ zip [0..]
      $ reverse
      $ sort l
  \end{lstlisting}
  \pause
  Und jetzt kann man noch einmal Currying anwenden und alle \lstinline{$} zu \lstinline{.} machen:
  \begin{lstlisting}
    hIndex =
      length
      . takeWhile (\(i, n) -> n > i)
      . zip [0..]
      . reverse
      . sort
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{splitWhen}}
  \begin{lstlisting}
    splitWhen :: (a -> Bool) -> [a] -> ([a],[a])
    --- Beispiel:
    splitWhen even [1,2,3] = ([1],[2,3])
  \end{lstlisting}
  Zerlegt eine Liste an der Stelle, an der ein Prädikat zum ersten Mal erfüllt ist.
  \pause
  \begin{lstlisting}
    splitWhen p [] = ([],[])
    splitWhen p (x:xs)
      | p x       = ([],x:xs)
      | otherwise = let (ys,zs) = splitWhen p xs in (x:ys,zs)
  \end{lstlisting}
\end{frame}

\begin{frame}[fragile]
  \frametitle{\lstinline{group}}
  \begin{lstlisting}
    group :: Eq a => [a] -> [[a]]
    -- Beispiel:
    group "aaaBBcDDDD" = ["aaa","BB","c","DDDD"]
  \end{lstlisting}
  Gruppiert aufeinanderfolgende gleiche Elemente einer Liste.
  Verwende \lstinline{splitWhen}.
  \pause
  \begin{lstlisting}
    group [] = []
    group (x:xs) = let (group1,rest) = splitWhen (/=x) xs
                   in (x:group1) : group rest
  \end{lstlisting}
  (\lstinline{Eq a => ...} kam in der Vorlesung noch nicht dran,
  deshalb belassen wir es für heute bei:
  \lstinline{group} ist für jeden Typ \lstinline{a} definiert,
  den man mit \lstinline{==} und \lstinline{/=} vergleichen kann.)
\end{frame}

\end{document}
